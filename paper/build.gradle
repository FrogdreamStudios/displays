plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.0.0-beta17"
}

group = 'com.inotsleep'

configurations {
    shadow
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT")

    shadow 'me.inotsleep:utils:1.4.9'
    shadow 'com.github.zafarkhaja:java-semver:0.10.2'
    shadow 'com.google.code.gson:gson:2.13.1'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.named('jar') {
    enabled = false
}

tasks.named('build') {
    dependsOn shadowJar
}

shadowJar {
    configurations = [project.configurations.shadow]

    archiveClassifier = "paper"
    destinationDirectory.set(rootProject.layout.buildDirectory.dir("libs"))

    archiveBaseName = "displays-server-1_21_8"

    relocate("com.google.gson", "com.inotsleep.relocated.com.google.gson")
}