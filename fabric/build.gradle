plugins {
	id 'fabric-loom' version '1.13.3'
	id 'maven-publish'
	id 'com.gradleup.shadow' version '9.2.2'
}

group = "ru.l0sty.dreamdisplays"

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	implementation 'org.freedesktop.gstreamer:gst1-java-core:1.4.0'
	compileOnly 'net.java.dev.jna:jna:5.9.0'
	compileOnly 'net.java.dev.jna:jna-platform:5.9.0'
	implementation 'me.inotsleep:utils:1.3.1'

	implementation 'com.github.felipeucelli:javatube:6692a32'

	compileOnly("org.lwjgl:lwjgl:3.3.6")
}

shadowJar {
	// As default we use runtimeClasspath, but we can filter it
	dependencies {
		include(dependency('org.freedesktop.gstreamer:gst1-java-core'))
		exclude(dependency('net.java.dev.jna:jna'))
		exclude(dependency('net.java.dev.jna:jna-platform'))
		include(dependency('com.github.felipeucelli:javatube'))
		include(dependency('org.json:json'))
		include(dependency('me.inotsleep:utils'))
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	filesMatching("version") {
		expand "version": project.version
	}
}

compileJava {
	options.encoding = 'UTF-8'
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.

    // If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21

}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

jar {
	from(rootProject.file('LICENSE'))
}

remapJar {
	dependsOn shadowJar
	input.set(shadowJar.archiveFile)

	archiveClassifier = ""
	destinationDirectory.set(rootProject.layout.buildDirectory.dir("libs"))

	archiveBaseName = "dreamdisplays-fabric"
	archiveVersion = rootProject.version
}